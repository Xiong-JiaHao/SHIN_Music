apply plugin: 'com.android.application'

//设置发布的显示的版本名
def getVersionName() {
    def versionFile = file('version.properties')// 读取第一步新建的文件
    if (versionFile.canRead()) {// 判断文件读取异常
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps['CODE'].toInteger()// 读取文件里面的版本号
        versionProps['CODE'] = (++versionCode).toString()
        versionProps.store(versionFile.newWriter(), null)
        def first = ((versionCode - (versionCode % 10000)) / 10000) + 1
        versionCode = versionCode % 10000
        def thrid = versionCode % 1000
        def second = (versionCode - thrid) / 10
        return first + "." + second + "." + thrid // 返回自增之后的版本号
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

//设置发布的显示的版本号
def getVersionCode() {
    return new Date().format("yyyyMMddHH").toInteger()
}

android {
    compileSdkVersion 26

    def currentVersionCode = getVersionCode()
    def currentVersionName = getVersionName()

    defaultConfig {
        applicationId "com.gin.xjh.shin_music"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode currentVersionCode
        versionName currentVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    //Bmob的数据服务SDK
    implementation 'cn.bmob.android:bmob-sdk:3.6.3'
    //Bmob的消息推送SDK
    implementation 'cn.bmob.android:bmob-push:1.0.2'
    //动态申请权限
    annotationProcessor 'com.zhy:mpermission-compiler:1.0.0'
    implementation 'com.zhy:mpermission-api:1.0.0'
    //图片加载Picasso库
    implementation('com.squareup.picasso:picasso:2.71828') {
        exclude group: 'com.squareup.okio'
    }
    //网络请求Retrofit库
    implementation('com.squareup.retrofit2:retrofit:2.4.0') {
        exclude group: 'com.squareup.okio'
    }
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
}